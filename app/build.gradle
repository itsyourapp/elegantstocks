apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = 1
def versionMinor = 1
def versionPatch = project.hasProperty('buildNumber') ? project.getProperties().get('buildNumber').toInteger() : 0

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            storeFile file("${project.rootDir}/${keystoreProperties['storeFile']}")
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
        staging {
            keyAlias keystoreProperties['keyAlias']
            storeFile file("${project.rootDir}/${keystoreProperties['storeFile']}")
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
        beta {
            keyAlias keystoreProperties['keyAlias']
            storeFile file("${project.rootDir}/${keystoreProperties['storeFile']}")
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "app.itsyour.elegantstocks"
        minSdkVersion 25
        targetSdkVersion 28

        // Version
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch
        resValue "string", "build_number", "Version ${versionName}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        release {
            resValue "string", "app_name", "Elegant Stocks"
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-release.pro'
        }
        staging {
            resValue "string", "app_name", "Elegant Stocks"
            signingConfig signingConfigs.staging
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-staging.pro'
        }
        beta {
            signingConfig signingConfigs.beta
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".beta"
            versionNameSuffix "-BETA"
            resValue "string", "app_name", "Elegant Stocks Beta"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-beta.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            resValue "string", "app_name", "Elegant Stocks Debug"
            applicationIdSuffix ".debug"
            versionNameSuffix = "-DEBUG"
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "failed", "skipped"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidXVersion"
    implementation "androidx.fragment:fragment-ktx:$androidXVersion"
    implementation "com.google.android.material:material:$androidXVersion"
    implementation "androidx.recyclerview:recyclerview:$androidXVersion"
    implementation "androidx.core:core-ktx:$androidXVersion"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'

    // Dagger 2
    implementation 'com.google.dagger:dagger:2.16'
    implementation 'com.google.dagger:dagger-android:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.16'
    kapt 'com.google.dagger:dagger-android-processor:2.16'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    // Stetho
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.1.1'
    implementation 'com.jakewharton.rx2:replaying-share:2.0.1'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'


    // Room
    implementation "androidx.room:room-runtime:2.0.0-beta01"
    implementation "androidx.room:room-rxjava2:2.0.0-beta01"
    kapt "androidx.room:room-compiler:2.0.0-beta01"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.0'

    // Time
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

    // UI
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'net.opacapp:multiline-collapsingtoolbar:27.1.1'
    implementation 'com.balysv:material-ripple:1.0.2'
    implementation 'com.gordonwong:material-sheet-fab:1.2.1'

    // Testing
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha3'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlin_version}"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    testImplementation "org.mockito:mockito-core:2.18.3"
    testImplementation "org.robolectric:robolectric:3.8"
    testImplementation "org.robolectric:shadows-multidex:3.8"
}
